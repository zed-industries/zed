("(" @open ")" @close)
("[" @open "]" @close)
("{" @open "}" @close)
(("\"" @open "\"" @close) (#set! rainbow.exclude))
(("`" @open "`" @close) (#set! rainbow.exclude))
(("do" @open "done" @close) (#set! newline.only) (#set! rainbow.exclude))
((case_statement ("in" @open "esac" @close)) (#set! newline.only) (#set! rainbow.exclude))
((if_statement (elif_clause ("then" @open)) (else_clause ("else" @close))) (#set! newline.only) (#set! rainbow.exclude))
((if_statement (else_clause ("else" @open)) "fi" @close) (#set! newline.only) (#set! rainbow.exclude))
((if_statement ("then" @open) (elif_clause ("elif" @close))) (#set! newline.only) (#set! rainbow.exclude))
((if_statement ("then" @open) (else_clause ("else" @close))) (#set! newline.only) (#set! rainbow.exclude))
((if_statement ("then" @open "fi" @close)) (#set! newline.only) (#set! rainbow.exclude))
